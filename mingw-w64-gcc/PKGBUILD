# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

pkgname=mingw-w64-gcc
pkgver=6.2.1
_commit=c2103c17
_islver=0.17.1
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('zlib' 'mpc-devel'
	 'mingw-w64-crt'
	 'mingw-w64-binutils'
	 'mingw-w64-headers'
	 'mingw-w64-winpthreads-static')
makedepends=('git')
#checkdepends=('dejagnu') # Windows executables could run on Arch through bin_mft and Wine
optdepends=()
provides=('mingw-w64-gcc-base')
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("git://gcc.gnu.org/git/gcc.git#commit=${_commit}"
	"http://isl.gforge.inria.fr/isl-${_islver}.tar.bz2")
md5sums=('SKIP'
         '5184f543a013165e8057be59728664b1')

prepare() {
  cd "$srcdir"/gcc
  # link isl for in-tree builds
  ln -s ../isl-${_islver} isl
}

build() {
  for _target in ${_targets}; do
    mkdir -p "$srcdir"/gcc-build-${_target} && cd "$srcdir"/gcc-build-${_target}

    "$srcdir"/gcc/configure --prefix=/usr --libexecdir=/usr/lib \
        --target=${_target} \
        --enable-languages=c,lto,c++,objc,obj-c++,fortran \
        --disable-shared \
        --enable-static \
        --enable-threads=posix \
        --enable-fully-dynamic-string \
        --enable-libstdcxx-time=yes \
        --with-system-zlib \
        --enable-cloog-backend=isl \
        --enable-lto \
        --disable-dw2-exceptions \
        --disable-multilib \
        --enable-checking=release
    make all
  done
}

package() {
  for _target in ${_targets}; do
    cd "$srcdir"/gcc-build-${_target}
    make DESTDIR="$pkgdir" install
    strip "$pkgdir"/usr/bin/${_target}-*
    strip "$pkgdir"/usr/lib/gcc/${_target}/${pkgver}/{cc1*,collect2,f951,lto*}
    ln -s ${_target}-gcc "$pkgdir"/usr/bin/${_target}-cc
  done
  # remove unnecessary files
  rm -r "$pkgdir"/usr/share
  rm -f "$pkgdir"/usr/lib/libcc1.so*
  rm -f "$pkgdir"/usr/lib/libcc1.a
}
