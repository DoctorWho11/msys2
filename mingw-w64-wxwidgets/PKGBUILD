# Maintainer: Ivan Puntiy <ivan.puntiy-at-gmail>
# Contributor: Schala

pkgname=mingw-w64-wxwidgets-static
epoch=1
pkgver=3.1.0
pkgrel=1
pkgdesc="Win32 implementation of wxWidgets API for GUI (mingw-w64)"
arch=(any)
url="http://wxwidgets.org"
license=("custom:wxWindows")
makedepends=(mingw-w64-configure)
depends=("mingw-w64-crt"
         "mingw-w64-cppunit"
         "mingw-w64-libpng-static"
         "mingw-w64-libjpeg-turbo-static"
         "mingw-w64-expat-static")
options=(staticlibs !strip !buildflags)
conflicts=(mingw-w64-wxmsw2.9 mingw-w64-wxwidgets-static)
provides=(mingw-w64-wxmsw2.9 mingw-w64-wxwidgets-static)
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/wxWidgets-${pkgver}.tar.bz2")
sha256sums=('e082460fb6bf14b7dd6e8ac142598d1d3d0b08a7b5ba402fdbf8711da7e66da8')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"


build() {  

  cd "${srcdir}/wxWidgets-$pkgver"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --enable-static \
      --disable-shared \
      --enable-iniconf \
      --enable-iff \
      --enable-permissive \
      --enable-unicode \
      --enable-graphics_ctx \
      --enable-accessibility \
      --enable-mediactrl \
      --disable-monolithic \
      --disable-mslu \
      --disable-precomp-headers \
      --with-msw \
      --with-opengl \
      --with-libpng=builtin \
      --with-libxpm=builtin \
      --with-libjpeg=builtin \
      --with-libtiff=builtin \
      --with-regex=builtin \
      --enable-webview
    make
    popd
  done
}

package() {
  mkdir -p "${pkgdir}/usr/bin"
  for _arch in ${_architectures}; do
    cd "${srcdir}/wxWidgets-${pkgver}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    
    # rm "${pkgdir}/usr/${_arch}/bin/"*.exe
    # rm "$pkgdir/usr/${_arch}/bin/wxrc-3.0"
    # rm -r "$pkgdir/usr/${_arch}/share"
  done
}
